PREFIX curric: <https://w3id.org/uk/curriculum/core/>
PREFIX eng: <https://w3id.org/uk/curriculum/england/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT
  ?contentId
  ?label
  ?definition
  ?subjectId
  ?subjectLabel
  (GROUP_CONCAT(DISTINCT ?keyStageId; separator=",") AS ?keyStages)
  ?strandLabel
  ?type
WHERE {
  # Get all content
  ?content a ?type ;
           skos:prefLabel ?label .

  OPTIONAL {
    ?content skos:definition ?definition .
  }

  # Get subject via scheme -> subsubject -> subject
  ?scheme curric:hasContentDescriptor ?content ;
          curric:isPartOf ?subsubject .

  ?subsubject curric:isPartOf ?subject .
  ?subject a curric:Subject ;
           rdfs:label ?subjectLabel .

  # Get key stage(s)
  OPTIONAL {
    ?scheme curric:hasKeyStage ?keyStage .
    BIND(STRAFTER(STR(?keyStage), "england/") AS ?keyStageId)
  }

  # Get strand (traverse: content -> substrand -> strand)
  OPTIONAL {
    ?content skos:broader ?substrand .
    ?substrand skos:broader ?strand .
    ?strand a curric:Strand ;
            skos:prefLabel ?strandLabel .
  }

  BIND(STRAFTER(STR(?content), "england/") AS ?contentId)
  BIND(STRAFTER(STR(?subject), "england/") AS ?subjectId)

  # Filter to content types only
  FILTER(?type IN (curric:ContentDescriptor, curric:ContentSubDescriptor))
}
GROUP BY ?contentId ?label ?definition ?subjectId ?subjectLabel ?strandLabel ?type
ORDER BY ?subjectLabel ?keyStages ?strandLabel ?label
